class FlappyGame {

    field int score;
    field String playerName;
    field int highestScore;
    field FloppyDisk disk;
    field int groundLevel;

    constructor FlappyGame new(String name) {
        let score = 0;
        let groundLevel = 240;
        let disk = FloppyDisk.new(256, 128);
        let playerName = name;
        do drawGround();
        return this;
    }
    
    method void draw() {
        do clearPlayScreen();
        do disk.draw();
        return;
    } 

    method void clearPlayScreen() {
        do Screen.setColor(false);
        do Screen.drawRectangle(0,0, 511, groundLevel);
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void drawGround() {
        do Screen.setColor(true);
        do Screen.drawRectangle(32,groundLevel, 511, 255);
        return;
    }

    method int getScore() {
        return score;
    }

    method void printScore() {
        do Output.moveCursor(22,0);
        do Output.printString(playerName);
        do Output.printChar(58);
        do Output.printInt(score);
        return;
    }

    method boolean run() {
        var Obstacle obstacle;
        let obstacle = Obstacle.new(200);

        while (true) {
            let score = score + 1;
            if (score > 9999) {
                return true;
            }
            do printScore();
            do disk.update();

            do obstacle.move();

            do draw();
            do obstacle.draw();
            if (disk.getBottomY() > groundLevel) { return false; }
            do Sys.wait(15);
        }
        return false;
    }
}