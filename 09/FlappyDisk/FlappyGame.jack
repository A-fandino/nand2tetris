class FlappyGame {

    field int score;
    field String playerName;
    field int highestScore;
    field FloppyDisk disk;
    field int groundLevel;
    field Obstacle obstacle;

    constructor FlappyGame new(String name) {
        let score = 0;
        let groundLevel = 240;
        let disk = FloppyDisk.new(256, 128);
        let playerName = name;
        let obstacle = Obstacle.new(511);
        do drawGround();
        return this;
    }
    
    method void draw() {
        do clearPlayScreen();
        do disk.draw();
        return;
    } 

    method void clearPlayScreen() {
        do Screen.setColor(false);
        do Screen.drawRectangle(0,0, 511, groundLevel);
        return;
    }

    method void dispose() {
        do disk.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void drawGround() {
        do Screen.setColor(true);
        do Screen.drawRectangle(32,groundLevel, 511, 255);
        return;
    }

    method int getScore() {
        return score;
    }

    method void printScore() {
        do Output.moveCursor(22,0);
        do Output.printString(playerName);
        do Output.printChar(58);
        do Output.printInt(score);
        return;
    }


    method void run() {
        var boolean result;
        let result = mainLoop();
        if (result) {
            do Output.moveCursor(22,0);
            do Output.printString(playerName);
            do Output.printString(" wins!");
        } else {
            do Output.moveCursor(22,0);
            do Output.printString(playerName);
            do Output.printString(" loses!");
        }
        return;
    }

    method boolean mainLoop() {
        while (true) {
            let score = score + 1;
            if (score > 5000) { return true; }
            do printScore();
            do disk.update();

            do obstacle.move();

            do draw();
            do obstacle.draw();
            if (obstacle.collidesWith(disk.getX(), disk.getY(), disk.getX2(), disk.getY2())) {
                return false;
            }
            if (disk.getY2() > groundLevel) { return false; }
            do Sys.wait(15);
        }
        return false;
    }

    
}