class FlappyGame {

    field int score;
    field String playerName;
    field int highestScore;
    field FloppyDisk disk;
    field int groundLevel;
    field Obstacle obstacle;

    constructor FlappyGame new(String name) {
        let score = 0;
        let groundLevel = 240;
        let disk = FloppyDisk.new(256, 128);
        let playerName = name;
        let obstacle = Obstacle.new(511);
        do drawGround();
        return this;
    }
    
    method void draw() {
        do clearPlayScreen();
        do disk.draw();
        return;
    } 

    method void clearPlayScreen() {
        do Screen.setColor(false);
        do Screen.drawRectangle(0,0, 511, groundLevel);
        return;
    }

    method void clearScreenAnimated() {
        var int memAddress, limit;
        let memAddress = 16384;
        let limit = 16384 + 8192;
        while (memAddress < limit) {
            do Memory.poke(memAddress, 0);
            let memAddress = memAddress + 1;
        }
        return;
    }

    method void dispose() {
        do disk.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void drawGround() {
        do Screen.setColor(true);
        do Screen.drawRectangle(32,groundLevel, 511, 255);
        return;
    }

    method int getScore() {
        return score;
    }

    method void printScore() {
        do Output.moveCursor(1,1);
        do Output.printString(playerName);
        do Output.printChar(58);
        do Output.printInt(score);
        return;
    }

    /**
    * Run the game and show the results
    */
    method void run() {
        var boolean result;
        let result = mainLoop();
        do Sys.wait(500);
        do clearScreenAnimated();
        do Output.moveCursor(10,20);
        if (result) {
            do Output.printString(playerName);
            do Output.printString(" wins!");
            return;
        } 
        do Output.printString("Thanks for playing, ");
        do Output.printString(playerName);
        do Output.printString("!");
        do Output.moveCursor(11, 20);
        do Output.printString("Your score was: ");
        do Output.printInt(score);
        return;
    }

    /**
    * Main game loop
    * @return true if the player won, false if the player quit
    */
    method boolean mainLoop() {
        var int key;
        while (~(key = 81)) { //while not 'q' pressed
            let score = score + 1;
            if (score > 1000) { return true; }
            
            //Update step
            do disk.update();
            do obstacle.move();

            //Draw step
            do draw();
            do obstacle.draw();
            do printScore();

            // Decide if we need to reset the game
            if ((disk.getY2() > groundLevel) | 
                obstacle.collidesWith(disk.getX(), disk.getY(), disk.getX2(), disk.getY2())) {
                do resetGame();
            }

            if (PseudoRandom.modulo(score, 250) = 0) {
                do obstacle.increaseSpeed();
            }
            let key = Keyboard.keyPressed();
            do Sys.wait(15);
        }
        return false;
    }

    method void resetGame() {
        let score = 0;
        do disk.reset();
        do obstacle.reset();
        return;
    }

    
}